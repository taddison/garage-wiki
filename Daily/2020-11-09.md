# November 9, 2020

## Reference data in Kusto, via storage account

- Create a storage account and container
- Export an initial set of data (https://docs.microsoft.com/en-us/azure/data-explorer/kusto/management/data-export/export-data-to-storage)

```kql
.export to csv (
 h@'https://{ACCOUNT}.blob.core.windows.net/{CONTAINER};{ACCOUNT_KEY}'
) with (
  includeHeaders=all
)
<| let allComputers = EventLog
| where Timestamp > ago(1d)
| distinct ComputerName
| extend Team = 'Unknown', Group = 'Unknown';
allComputers
```

- Create an external table (https://docs.microsoft.com/en-us/azure/data-explorer/kusto/management/external-tables-azurestorage-azuredatalake)

```kql
.create-or-alter external table ComputerMetadata_External (ComputerName: string, Team: string, Group:string)
kind=blob
dataformat=csv
(
   h@'https://{ACCOUNT}.blob.core.windows.net/{CONTAINER}/{FILENAME}.csv;{ACCOUNT_KEY}'
) with (
  includeHeaders = all
)
```

- Import to a table with set-or-replace (https://docs.microsoft.com/en-us/azure/data-explorer/kusto/management/data-ingestion/ingest-from-query)

```kql
.set-or-replace ComputerMetadata
<| external_table('ComputerMetadata_External')
```

- Re-export with a lookup that includes data from both recent history AND the table

```kql
.export to csv (
 h@'https://{ACCOUNT}.blob.core.windows.net/{CONTAINER};{ACCOUNT_KEY}'
) with (
  includeHeaders=all
)
<| let allComputers = Event
| where Timestamp > ago(1d)
| distinct ComputerName
| join kind = fullouter (ComputerMetadata) on ComputerName
| project ComputerName = coalesce(ComputerName, ComputerName1), Team = coalesce(Team, 'Unknown'), Group = coalesce(Group, 'Unknown');
allComputers
```
