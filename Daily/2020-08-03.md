# August 3, 2020

Forecasting table growth with Kusto.  Assumes that data is loaded into a `TableSizeHistory` table at least once per-day, with a minimum of:
- ServerName
- DatabaseName
- SchemaName
- TableName
- ReservedMB

It is assumed that databases in availability groups are loaded with the AG listener as the server name (and so won't appear multiple times).

The query looks at the last 4 weeks to forecast the growth over the next 4 weeks, and reports on the top 100 tables by overall growth.

If used in a Kusto dashboard the parameter `databaseName` can be used a multi-select filter.

The data reported is the increase from the _baseline_, defined as the size 28 days ago.


```kql
let ['databaseName']='';
let lookbackDays = 28;
let minTableSize = 1000;
let minGrowthSize = 1000;
let lookbackAgo = lookbackDays * 1d;
let localDbs = TableSizeHistory
| where TimeGenerated > ago(lookbackAgo)
| summarize dcount(ServerName) by DatabaseName
| where dcount_ServerName > 1
| summarize make_list(DatabaseName)
;
let oldestSpaceReport = TableSizeHistory
| where TimeGenerated > ago(lookbackAgo)
| where isempty(['databaseName']) or DatabaseName in (['databaseName'])
| extend IsLocal=iif(DatabaseName in (localDbs),true,false)
| extend Name = strcat(iif(IsLocal,ServerName,""), DatabaseName,"-",SchemaName,"-",TableName)
| summarize (MinDate, MinMB)=arg_min(TimeGenerated, ReservedMB), (MaxDate, MaxMB) = arg_max(TimeGenerated, ReservedMB) by Name
;
let topGrowthSeries = TableSizeHistory
| where isempty(['databaseName']) or DatabaseName in (['databaseName'])
| where ReservedMB != 0 and ReservedMB > minTableSize
| extend IsLocal=iif(DatabaseName in (localDbs),true,false)
| extend Name = strcat(iif(IsLocal,ServerName,""), DatabaseName,"-",SchemaName,"-",TableName)
| join kind=leftouter oldestSpaceReport on Name
| extend IncreaseFromBaselineMB = ReservedMB - MinMB
// Default of null to prevent the series hugging the y-axis (0) after today
| make-series Size=max(IncreaseFromBaselineMB) default=double(null) on TimeGenerated in range(ago(lookbackAgo), ago(-lookbackAgo), 1d) by Name
| extend Size_Forecast = series_decompose_forecast(Size, lookbackDays)
| extend (minSize, minSizeIndex, maxSize, maxSizeIndex, avgSize, varianceSize, stdevSize) = series_stats(Size_Forecast)
// Exclude tables that are shrinking
| where maxSizeIndex > minSizeIndex
| where maxSize - minSize > minGrowthSize
// Comment out to view the chart of actual data instead
| project-away Size
;
topGrowthSeries
| join oldestSpaceReport on Name
| project Name, MinMB, MaxMB, GrowthToDateMB=MaxMB-MinMB, ForecastMaxMB=round(MinMB+maxSize,0)
| order by GrowthToDateMB desc
| take 100
;
```